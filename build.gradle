plugins {
    id 'java'
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version "1.4.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    maven {
        name = "Modmuss50"
        url = "https://maven.fabricmc.net/"
    }
	mavenLocal()
}

def ENV = System.getenv()
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersionName()
logger.lifecycle("Version = " + version)

dependencies {
    minecraft "com.mojang:minecraft:1.16.4"
    mappings "net.fabricmc:yarn:1.16.4+build.7:v2"
    modImplementation "net.fabricmc:fabric-loader:0.10.8"

    //Fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.27.1+1.16"
}

group = 'SimpleVoidWorld'

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

curseforge {
    if (ENV.CURSE_API_KEY) {
        apiKey = ENV.CURSE_API_KEY
    }
    project {
        id = '254466'
        changelog = 'A changelog can be found at https://github.com/modmuss50/SimpleVoidWorld'
        releaseType = 'release'
        addGameVersion '1.16.4'
        addGameVersion 'Fabric'
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
        forgeGradleIntegration = false
    }
}