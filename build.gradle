plugins {
    id 'java'
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version "1.4.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17


def ENV = System.getenv()

version = "6.0.0"

dependencies {
    minecraft "com.mojang:minecraft:1.18.2"
    mappings "net.fabricmc:yarn:1.18.2+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.13.3"

    //Fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.47.9+1.18.2"
}

group = 'SimpleVoidWorld'

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

curseforge {
    if (ENV.CURSE_API_KEY) {
        apiKey = ENV.CURSE_API_KEY
    }
    project {
        id = '254466'
        changelog = 'A changelog can be found at https://github.com/modmuss50/SimpleVoidWorld'
        releaseType = 'release'
        addGameVersion '1.18.2'
        addGameVersion 'Fabric'
        mainArtifact remapJar
    }
    options {
        forgeGradleIntegration = false
    }
}